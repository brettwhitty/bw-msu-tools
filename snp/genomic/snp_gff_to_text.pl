#!/opt/rocks/bin/perl

## converts the GFF generated by snp_to_codon into a text table
##
## Brett Whitty
## whitty@msu.edu

use strict;
use warnings;

my @cols = (
    'org',
    'ref_genome', 
    'snp_genome',
    'seqid',
    'pos',
#    'strand', ## SNP is strandless
    'Parent',
    'ID',
    'region_code',
    'region_offset',
    'codon_strand',
    'ref_base',
    'snp_base',
    'ref_codon',
    'snp_codon',
    'ref_aa',
    'snp_aa',
    'ref_ambiguous',
    'snp_ambiguous',
    'synonymous',
);
print "## ".join("\t", @cols)."\n";

while (<>) {
    chomp;

    my @t = split(/\t/, $_);

    if (scalar(@t) != 9) { next; }
    
    my $atts = {};
    
    ($atts->{'seqid'}, $atts->{'pos'}, $atts->{'strand'}) = @t[0,3,5];
    
    my @attribs = split(/;/, $t[8]);
    
    foreach my $attrib(@attribs) {
        my ($key, $value) = split(/=/, $attrib);
#        my @values = split(/,/, $value);
#        $atts->{$key} = \@values;
        $atts->{$key} = $value;
    }
    my @line = ();
    foreach my $col(@cols) {
        push(@line, (defined $atts->{$col}) ? $atts->{$col} : '-');
    }
    print join("\t", @line)."\n";
}
